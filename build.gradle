import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  alias(libs.plugins.kotlinJvm) apply false
  alias(libs.plugins.spotless)
  alias(libs.plugins.mavenPublish) apply false
  alias(libs.plugins.dokka)
}

dependencies {
  dokka(projects.core)
  dokka(projects.environment)
}

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*"
    ktfmt(libs.ktfmt.get().version).googleStyle()
    trimTrailingWhitespace()
    endWithNewline()
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    maven { url "https://cache-redirector.jetbrains.com/maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-ide-plugin-dependencies" }
  }
}

subprojects {
  tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = '11'
    targetCompatibility = '11'
  }

  tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
      jvmTarget = '11'
    }
  }

  tasks.withType(Test).configureEach {
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }

  configurations.configureEach {
    exclude group: 'com.jetbrains.rd'
    exclude group: 'com.github.jetbrains', module: 'jetCheck'
    exclude group: 'com.jetbrains.infra'
    exclude group: 'org.roaringbitmap'
    exclude group: 'ai.grazie.spell'
    exclude group: 'ai.grazie.utils'
    exclude group: 'ai.grazie.nlp'
    exclude group: 'ai.grazie.model'
  }
}
