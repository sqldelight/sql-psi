plugins {
  alias(libs.plugins.kotlinJvm) apply false
  alias(libs.plugins.spotless)
  alias(libs.plugins.mavenPublish)
  alias(libs.plugins.dokka)
}

spotless {
  kotlin {
    target "**/*.kt"
    targetExclude "**/gen/**/*.*"
    ktlint('0.49.1').editorConfigOverride([
      "indent_size": "2",
       "disabled_rules": "package-name",
       "ij_kotlin_allow_trailing_comma": "true",
       "ij_kotlin_allow_trailing_comma_on_call_site": "true",
    ])
    trimTrailingWhitespace()
    endWithNewline()
  }
}

allprojects {
  repositories {
    mavenCentral()
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    maven { url "https://cache-redirector.jetbrains.com/maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-ide-plugin-dependencies" }
  }

  group = GROUP
  version = VERSION_NAME
}

subprojects {
  apply(plugin: "org.jetbrains.kotlin.jvm")

  kotlin.jvmToolchain(17)

  tasks.withType(Test).configureEach {
    testLogging {
      events = ["failed", "skipped", "passed"]
      exceptionFormat "full"
    }
  }

  configurations.configureEach {
    exclude group: 'com.jetbrains.rd'
    exclude group: 'com.github.jetbrains', module: 'jetCheck'
    exclude group: 'com.jetbrains.infra'
    exclude group: 'org.roaringbitmap'
    exclude group: 'ai.grazie.spell'
    exclude group: 'ai.grazie.utils'
    exclude group: 'ai.grazie.nlp'
    exclude group: 'ai.grazie.model'
  }
}
