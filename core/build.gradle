plugins {
  alias(libs.plugins.kotlinJvm)
  alias(libs.plugins.grammarKitComposer)
  id("java-test-fixtures")
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"

grammarKit {
  intellijRelease.set(libs.versions.ideaVersion)
}

sourceSets {
  main.java.srcDir "gen"
}

dependencies {
  compileOnly libs.bundles.intelliJ

  testFixturesApi project(":core")
  testFixturesApi libs.junit
  testFixturesCompileOnly libs.bundles.intelliJ

  testImplementation libs.junit
  testImplementation libs.truth
  testImplementation(libs.bundles.intelliJ) {
    exclude(group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-core")
  }
  testImplementation libs.coroutines.core
}

generateLexer {
  sourceFile.set(project.file('src/main/kotlin/com/alecstrong/sql/psi/core/SqlLexer.flex'))
  targetDir.set('gen/com/alecstrong/sql/psi/core/lexer/')
  targetClass.set('SqlLexer')
  purgeOldFiles.set(true)
}

compileKotlin.dependsOn generateLexer
kotlinSourcesJar.dependsOn generateLexer
